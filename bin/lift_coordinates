#!/usr/bin/env nextflow

map = file(params.map)
ped = file(params.ped)

params.out = '.'

process map2bed {

    input:
        file MAP from map

    output:
        file 'out.bed' into bed

    script:
    template 'io/map2bed.py'

}

process download_chain {

    input:
        val OLD from params.old
        val NEW from params.new
    
    output:
        file "${OLD}To${NEW}.over.chain.gz" into chain

    """
    wget http://hgdownload.cse.ucsc.edu/goldenPath/${OLD}/liftOver/${OLD}To${NEW.capitalize()}.over.chain.gz
    """

}

process lift {

    input:
        file BED from bed
        file CHAIN from chain

    output:
        file 'lifted.bed' into lifted_bed
        file 'unlifted' into unlifted

    """
    liftOver ${BED} ${CHAIN} lifted.bed unlifted
    """

}

process bed2map {

    publishDir "$params.out", overwrite: true, mode: "copy"

    input:
        file BED from lifted_bed

    output:
        file 'out.map'

    script:
    template 'io/bed2map.py'

}

process remove_unlifted {

    publishDir "$params.out", overwrite: true, mode: "move"

    input:
        file PED from ped
        file MAP from map
        file EXCLUDE from unlifted

    output:
        file 'out.ped'

    """
    cut -f4 ${EXCLUDE}| grep -v ^# >excluded
    plink --ped ${PED} --map ${MAP} --exclude excluded --recode --out out
    """
}